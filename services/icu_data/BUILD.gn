# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application.gni")
import("//mojo/public/tools/bindings/mojom.gni")

action("embed_icu_data") {
  script = "embed_icu_data.py"

  inputs = [
    "$root_build_dir/icudtl.dat",
  ]

  outputs = [
    "$root_gen_dir/mojo/icu_data/data.cc",
  ]

  args = rebase_path(inputs + outputs, root_build_dir)

  deps = [
    "//third_party/icu:icudata",
  ]
}

mojo_native_application("icu_data") {
  sources = [
    "icu_data_impl.cc",
    "data.h",
    "$root_gen_dir/mojo/icu_data/data.cc",
  ]

  deps = [
    "//base",
    "//mojo/application",
    "//mojo/common",
    "//mojo/environment:chromium",
    ":embed_icu_data",
    ":interfaces",
  ]
}

mojom("interfaces") {
  sources = [
    "icu_data.mojom",
  ]
}
